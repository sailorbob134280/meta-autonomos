# Start with a base Poky distro and go from there
require conf/distro/poky.conf

DISTRO = "autonomos"
DISTRO_NAME = "AutonomOS"
DISTRO_VERSION = "0.1"
DISTRO_CODENAME = "Rubik"

# Use standard image naming for RAUC bundle compatibility
# Bundle expects: ${IMAGE_BASENAME}${IMAGE_MACHINE_SUFFIX}${IMAGE_NAME_SUFFIX}.${fstype}
# e.g., autonomos-devel-raspberrypi5.rootfs.ext4
IMAGE_MACHINE_SUFFIX = "-${MACHINE}"
IMAGE_NAME_SUFFIX = ".rootfs"

# Use systemd over sysvinit (usrmerge required for systemd in scarthgap+)
DISTRO_FEATURES:append = " systemd usrmerge"
DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"
VIRTUAL-RUNTIME_network_manager = "networkd"

# Strip out unnecessary graphical support
DISTRO_FEATURES:remove = " x11 wayland"

# Use IPK package format
PACKAGE_CLASSES = "package_ipk"

# Require wifi
DISTRO_FEATURES:append = " wifi bluetooth"

# Support containerization
DISTRO_FEATURES:append = "${@bb.utils.contains('AUTONOMOS_FEATURES', 'containers', ' virtualization', '', d)}"
DISTRO_FEATURES:append = "${@bb.utils.contains('AUTONOMOS_FEATURES', 'kubernetes', ' k3s seccomp virtualization', '', d)}"

# Fix the command line if kubernetes is enabled
CMDLINE_ROOTFS:append = "${@bb.utils.contains('AUTONOMOS_FEATURES', 'kubernetes', ' cgroup_enable=cpuset cgroup_memory=1 cgroup_enable=memory ', '', d)}"

# Read-only rootfs support (opt-in)
IMAGE_FEATURES:append = "${@bb.utils.contains('AUTONOMOS_FEATURES', 'read-only-rootfs', ' read-only-rootfs', '', d)}"

# RAUC OTA support
DISTRO_FEATURES:append = " rauc"
IMAGE_INSTALL:append = " rauc casync"

# Partition size configuration for A/B rootfs layout (in MB)
# These control WKS partition sizes and can be overridden in local.conf or project kas configs
AUTONOMOS_ROOTFS_SIZE ?= "2048"
AUTONOMOS_DATA_SIZE ?= "512"

# Constrain ext4 image size to match partition size for RAUC compatibility
# Default 1.3 (30% overhead) is excessive and causes images larger than partition slots
IMAGE_OVERHEAD_FACTOR = "1.1"
IMAGE_ROOTFS_SIZE = "${@int(d.getVar('AUTONOMOS_ROOTFS_SIZE')) * 1024}"
IMAGE_ROOTFS_MAXSIZE = "${@int(d.getVar('AUTONOMOS_ROOTFS_SIZE')) * 1024}"

# Reproducible ext4 images for efficient delta updates (casync)
# Random UUID and hash_seed cause cascading chunk differences even with identical file content
# See: https://reproducible-builds.org/docs/system-images/
EXTRA_IMAGECMD:ext4 = "-i 4096 -U d4b90c75-f482-4de5-b1d6-c0b42a9ad80a -E hash_seed=1e92a9fe-b627-4579-9e1e-e95e6f0e84d0"

# Set the C standard library to use
TCLIBC = "glibc"
